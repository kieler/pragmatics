grammar de.cau.cs.kieler.kiml.grana.text.Grana
hidden(WS, ML_COMMENT, SL_COMMENT)

generate grana "http://www.cau.de/cs/kieler/kiml/grana/text/Grana"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://kieler.cs.cau.de/KLayoutData"
import "http://kieler.cs.cau.de/KGraph"  

Grana:
    ('globalResources' (globalResources+=GlobalResourceRef)*)?
    
    (jobs+=Job)+
;

Job: {Job}
    'job' (name=ID)?
    
    (layoutBeforeAnalysis ?= 'layoutBeforeAnalysis')?
    
    'resources'
        (resources+=Resource)*
        
    
    'layoutoptions'
        layoutOptions+=KIdentifier
        
    'analyses'
        (analyses+=Analysis)*
        
    'output'
        output=STRING 
;

Resource:
    ResourceReference | LocalResource
;

ResourceReference:
    'ref' (resourceRefs+=[GlobalResourceRef | ID])+
;

GlobalResourceRef:
    name=ID
    (resources+=LocalResource)
;

LocalResource:
    path=STRING
    ('filter' filter=STRING)
;

Analysis:
    name=QualifiedID
;


KIdentifier returns KIdentifier:
    {KIdentifier}
    id=ID
     '{' 
        (persistentEntries+=PersistentEntry (persistentEntries+=PersistentEntry)*)? 
     '}'
;

PersistentEntry returns PersistentEntry:
    key=QualifiedID ':' value=PropertyValue
;

QualifiedID returns ecore::EString:
    ID ('.' ID)*;


PropertyValue returns ecore::EString:
    BOOLEAN | STRING | Float | QualifiedID;
    

Float returns ecore::EFloat:
    TFLOAT | NATURAL;
    
    /* Terminals */

terminal TFLOAT returns ecore::EFloat:
    ('+' | '-') (
        ('0'..'9')+ ('.' ('0'..'9')*)? (('e' | 'E') ('+' | '-')? ('0'..'9')+)?
        | '.' ('0'..'9')+ (('e' | 'E') ('+' | '-')? ('0'..'9')+)?
    )
    | ('0'..'9')+ '.' ('0'..'9')* (('e' | 'E') ('+' | '-')? ('0'..'9')+)?
    | '.' ('0'..'9')+ (('e' | 'E') ('+' | '-')? ('0'..'9')+)?
    | ('0'..'9')+ ('e' | 'E') ('+' | '-')? ('0'..'9')+;
    
terminal NATURAL returns ecore::EInt:
    ('0'..'9')+;

terminal BOOLEAN returns ecore::EBoolean:
    'true' | 'false';
    
terminal STRING :
    // chsch: added '\'' alternative! 
    '"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|'\''|'\\') | !('\\'|'"') )* '"';

terminal ID:
    // chsch: the optional leading '^' is the implicit keyword escape character
    '^'? ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;

terminal ML_COMMENT : '/*' -> '*/';
terminal SL_COMMENT : '//' !('\n'|'\r')* ('\r'? '\n')?;
terminal WS         : (' '|'\t'|'\r'|'\n')+;
    
