grammar de.cau.cs.kieler.kiml.config.text.LayoutConfig
hidden(WS, ML_COMMENT, SL_COMMENT)


import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://elk.eclipse.org/KLayoutData"
import "http://elk.eclipse.org/KGraph"  

KGraphElement returns KGraphElement: 
    {KNode}
    (data+=KIdentifier)*
;

KIdentifier returns KIdentifier:
    {KIdentifier}
    id=ID
     '{' 
        (persistentEntries+=PersistentEntry (persistentEntries+=PersistentEntry)*)? 
     '}'
;

PersistentEntry returns PersistentEntry:
    key=QualifiedID ':' value=PropertyValue
;

PropertyValue returns ecore::EString:
    BOOLEAN | STRING | Float | QualifiedID;

QualifiedID returns ecore::EString:
    ID ('.' ID)*;

Float returns ecore::EFloat:
    TFLOAT | NATURAL;

/* Terminals */

terminal TFLOAT returns ecore::EFloat:
    ('+' | '-') (
        ('0'..'9')+ ('.' ('0'..'9')*)? (('e' | 'E') ('+' | '-')? ('0'..'9')+)?
        | '.' ('0'..'9')+ (('e' | 'E') ('+' | '-')? ('0'..'9')+)?
    )
    | ('0'..'9')+ '.' ('0'..'9')* (('e' | 'E') ('+' | '-')? ('0'..'9')+)?
    | '.' ('0'..'9')+ (('e' | 'E') ('+' | '-')? ('0'..'9')+)?
    | ('0'..'9')+ ('e' | 'E') ('+' | '-')? ('0'..'9')+;
    
terminal NATURAL returns ecore::EInt:
    ('0'..'9')+;

terminal BOOLEAN returns ecore::EBoolean:
    'true' | 'false';
    
terminal STRING :
    // chsch: added '\'' alternative! 
    '"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|'\''|'\\') | !('\\'|'"') )* '"';

terminal ID:
    // chsch: the optional leading '^' is the implicit keyword escape character
    '^'? ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;

terminal ML_COMMENT : '/*' -> '*/';
terminal SL_COMMENT : '//' !('\n'|'\r')* ('\r'? '\n')?;
terminal WS         : (' '|'\t'|'\r'|'\n')+;