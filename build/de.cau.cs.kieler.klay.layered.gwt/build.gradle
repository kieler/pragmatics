repositories {
    mavenCentral()
}

configurations {
    junitAnt
}

dependencies {
    junitAnt 'junit:junit:4.8.2'
    junitAnt('org.apache.ant:ant-junit:1.9.2') {
        transitive = false
    }
    junitAnt('org.apache.ant:ant-junit4:1.9.2') {
        transitive = false
    }
}

ant.taskdef(name: 'junit', classname: 'org.apache.tools.ant.taskdefs.optional.junit.JUnitTask', classpath: configurations.junitAnt.asPath)
ant.importBuild 'build.xml'

task npmInstall(type: Exec) {
    workingDir 'test/js'
    executable 'npm'
    args 'install'
}

task unzipKlayJs(type: Copy, dependsOn: plainjslinker) {
    from zipTree('target/klay_layered_js_plainjslinker_nightly_latest.zip')
    include 'klay.js'
    into 'test/js'
}

task qunit(type: Exec, dependsOn: [npmInstall, unzipKlayJs]) {
    workingDir 'test/js'
    executable 'grunt'
}

task cleanJs(type: Delete) {
    delete 'build'
    delete 'test/js/node_modules'
    delete 'test/js/build'
    delete 'test/js/klay.js'
}
